use alienfile;

use strict;
use warnings;
use Config;
requires 'Alien::gmake';

share {
	requires 'File::Copy::Recursive' => 0;
	requires 'Path::Tiny' => 0;
	requires 'HTTP::Tiny' => 0;
	requires 'Net::SSLeay' => 0;
	requires 'IO::Socket::SSL' => 0;
	requires 'Mojo::JSON';

	my $bit = defined $Config{'archname64'} ? 64 : 32;
	my %tags = (
		linux32   => { osname => 'linux'  , bit => 32 },
		linux64   => { osname => 'linux'  , bit => 64 },
		macosx64  => { osname => 'darwin' , bit => 64 },
		windows32 => { osname => 'MSWin32', bit => 32 },
		windows64 => { osname => 'MSWin32', bit => 64 },
	);

	my ($match_tag) = grep {
		$tags{$_}{osname} eq $^O
		&& $tags{$_}{bit} eq $bit
	} keys %tags;

	my $cdn_url = 'https://cef-builds.spotifycdn.com';
	start_url "$cdn_url/index.json";
	meta->around_hook(
		fetch => sub {
			my $orig = shift;
			my $build = shift;
			my $data = $orig->($build, @_);

			if( $data->{filename} =~ /\Qindex.json\E$/ ) {
				my $json_data = Mojo::JSON::decode_json(
					exists $data->{path}
					?  Path::Tiny::path($data->{path})->slurp_raw
					: $data->{content}
				);
				my $match_data = $json_data->{$match_tag};
				my @list = map {
						my $files = $_->{files};
						my ($min) = grep { $_->{type} eq 'minimal' } @$files;
						# client | standard | minimal
						my $name = $min->{name};
						{
							version => $_->{cef_version} . "+" . $_->{chromium_version},
							filename => $name,
							url => "$cdn_url/$name",
						}
					}
					grep { $_->{channel} eq 'stable' }
					# stable | beta
					@{ $json_data->{$match_tag}{versions} };

				return {
					type => 'list',
					list => \@list,
				};
			}

			return $data;
                },
        );
	plugin Download => (
		version => qr/^cef_binary_(.*)_${match_tag}_minimal\.tar\.bz2$/,
	);

	plugin 'Extract::CommandLine' => 'tar.bz2';

	patch sub {
		my ($build) = @_;

		my $cef_variables = Path::Tiny->new('cmake/cef_variables.cmake');
		$cef_variables->edit_lines(sub {
			s|\Qif(OS_WINDOWS)\E|if(OS_WINDOWS AND "\${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")|;
		});
		$cef_variables->append_utf8(
			Path::Tiny->new($build->install_prop->{patch}, 'cef_variables_mingw.cmake')->slurp_utf8
		);

		if( $^O eq 'MSWin32' ) {
			my $path_to_cef_atomicops_x86_gcc = 'include/base/internal/cef_atomicops_x86_gcc.h';
			my $response = HTTP::Tiny->new->get('https://bitbucket.org/chromiumembedded/cef/raw/master/'. $path_to_cef_atomicops_x86_gcc);
			die "Could not download cef_atomicops_x86_gcc.h!\n" unless $response->{success};
			my $cef_atomicops_x86_gcc = Path::Tiny->new($path_to_cef_atomicops_x86_gcc);
			$cef_atomicops_x86_gcc->spew_utf8($response->{content});
		}

		my $string = Path::Tiny->new("include/internal/cef_string_wrappers.h");
		$string->edit( sub {
			s/\Q#include <string>\E/$&\n#include <cstring>/s;
		});
	};

	build [
		(
			q|install_name_tool -id '@rpath/Chromium Embedded Framework.framework/Chromium Embedded Framework' 'Release/Chromium Embedded Framework.framework/Chromium Embedded Framework'|
		) x!!( $^O eq 'darwin' ),
		[ 'cmake', qw(-G), 'Unix Makefiles',
			'-DCMAKE_INSTALL_PREFIX:PATH=%{.install.prefix}',
			'.' ],
		[ '%{gmake}' ],
		sub {
			my ($build) = @_;

			my @dirs;

			$build->log("copy to prefix");
			my $prefix_abs = Path::Tiny::path($build->install_prop->{prefix});

			Path::Tiny::path('LICENSE.txt')->copy( $prefix_abs );

			my $icudtl = Path::Tiny::path('Resources/icudtl.dat');
			if( -f $icudtl ) {
				# The resource file `icudtl.dat` needs to be in
				# the same directory as the `Release/` library.
				# See <https://www.magpcss.org/ceforum/viewtopic.php?f=6&t=13176>,
				# <https://bitbucket.org/chromiumembedded/cef/issues/1398>.
				$icudtl->move('Release/icudtl.dat');
			}

			my $release_dir = $prefix_abs->child( 'Release' );
			$release_dir->mkpath;
			File::Copy::Recursive::rcopy_glob("libcef_dll_wrapper/libcef_dll_wrapper*", $release_dir );

			for my $subdir (qw(include Release Resources)) {
				my $target_dir = $prefix_abs->child( $subdir );
				$target_dir->mkpath;
				File::Copy::Recursive::dircopy( $subdir, $target_dir );
			}
		},
	];

	gather sub {
		my($build) = @_;
		my $prefix = $build->runtime_prop->{prefix};

		# Do not use the include subdirectory for CFLAGS since all
		# examples of using CEF API do the following for includes:
		# ```c
		# #include "include/cef_[...].h"
		# ```
		my $cflags = "-I$prefix";
		my @ldlibs = $^O ne 'darwin' ? ("-lcef") : ();
		my $libs = join " ", "-L$prefix/Release", @ldlibs;
		my $rpath = "Release";

		$build->runtime_prop->{cflags}  = $cflags;
		$build->runtime_prop->{libs}    = $libs;

		push @{ $build->runtime_prop->{rpath} }, $rpath;
	};
};

